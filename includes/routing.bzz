include "table.bzz"
include "includes/constants.bzz"


var hop_counts = {}


function init_hop_count() {
    neighbors.listen("hop", function(vid, value, neighbor_id) {
        hop_counts[neighbor_id] = value
    })
}

# Periodically use to forward most recent lowest hop count
function broadcast_lowest_hop_count() {
    neighbors.broadcast("hop", get_min_hop_count() + 1)
}

function update_hop_count() {
    connected_robots_hop = {}
    neighbors.foreach(function(neighbor_id, data) {
        connected_robots_hop[neighbor_id] = 1
    })

    to_delete = {}
    foreach(hop_counts, function(neighbor_id, data) {
        if (connected_robots_hop[neighbor_id] == nil) {
            to_delete[neighbor_id] = 1
        }
    })
    
    foreach(to_delete, function(neighbor_id, value) {
        hop_counts[neighbor_id] = nil
    })
}

function get_min_hop_count() {

    update_hop_count()

    if (id == 5) {
        return 0  # Base Station has 0 hop count to itself
    } else if (size(hop_counts) > 0) {
        return table.min(hop_counts)
    } else {
        return NUMBER_OF_ROBOTS
    }
}
